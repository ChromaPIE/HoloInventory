/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2014 - 2017 Dries K. Aka Dries007
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
 * the Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
 * IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

import groovy.json.*

// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.0.2"
if (System.getenv().BUILD_NUMBER != null) version += "." + System.getenv().BUILD_NUMBER

targetCompatibility = 1.7
sourceCompatibility = 1.7

group= "net.dries007.holoinventory"
archivesBaseName = "HoloInventory"

minecraft {
    version = "1.11.2-13.20.0.2235"
    runDir = "run"
    mappings = "snapshot_20170221"
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    doLast {
        def updateFile = new File('update.json')
        if (updateFile.exists())
        {
            def outStream = new ByteArrayOutputStream()
            def result = exec {
                executable = 'git'
                args = [ 'log', '-n', '1', "--format='%B'"]
                standardOutput = outStream
            }
            def fullLog = outStream.toString().replaceAll("^\\s*'\\s*|\\s*'\\s*\$", "")

            def json = new JsonSlurper().parseText(updateFile.getText())
            json['promos'][project.minecraft.version + '-latest'] = project.version
            json['promos'][project.minecraft.version + '-recomended'] = project.version
            if (!json.containsKey(project.minecraft.version)) json.put(project.minecraft.version, new HashMap<>())
            def version = json[project.minecraft.version]
            version.put(project.version, fullLog)
            updateFile.write JsonOutput.prettyPrint(JsonOutput.toJson(json))
        }
    }
}

task deobfJar(type: Jar, dependsOn: 'jar') {
    from "LICENSE.txt"
    from sourceSets.main.output
    classifier "dev"
    appendix = project.minecraft.version
}
sourceJar {
    from "LICENSE.txt"
    exclude("com/**")
    classifier "src"
    appendix = project.minecraft.version
}
jar {
    from "LICENSE.txt"
    exclude("com/**")
    appendix = project.minecraft.version
}
artifacts {
    archives deobfJar
}
